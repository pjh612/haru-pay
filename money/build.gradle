plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id("com.google.cloud.tools.jib") version "3.4.4"
}

group = 'com.haru'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    all*.exclude group: 'org.springframework.boot', module: 'logback-classic'
}

repositories {
    mavenCentral()
}

dependencies {
    var queryDslVersion = "6.6"
    implementation project(":common")
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation("io.github.openfeign.querydsl:querydsl-core:$queryDslVersion")
    implementation("io.github.openfeign.querydsl:querydsl-jpa:$queryDslVersion")
    annotationProcessor("io.github.openfeign.querydsl:querydsl-apt:$queryDslVersion:jpa")
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.redisson:redisson-spring-boot-starter:3.45.0'
    implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.0'
    implementation 'org.springframework.kafka:spring-kafka'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    runtimeOnly 'org.apache.logging.log4j:log4j-layout-template-json'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-tracing-bridge-otel'
    implementation 'io.opentelemetry:opentelemetry-exporter-zipkin'
    testRuntimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.15.2'
    testImplementation 'it.ozimov:embedded-redis:0.7.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

def querydslSrcDir = 'src/main/generated'

clean {
    delete file(querydslSrcDir)
}

tasks.withType(JavaCompile) {
    options.generatedSourceOutputDirectory = file(querydslSrcDir)
}

tasks.register("prepareKotlinBuildScriptModel") {}

jib {
  from {
        image = 'bellsoft/liberica-openjre-alpine:17'
        platforms {
            platform {
                architecture = "arm64"
                os = "linux"
            }
        } // 기본 이미지
    }
    to {
        image = rootProject.name + '-' + project.name + ":" + version
        tags = ['latest']
    }
}

tasks.register('buildDockerImage') {
    dependsOn 'build'
    finalizedBy 'jibDockerBuild'
}